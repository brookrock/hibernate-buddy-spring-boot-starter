plugins {
	id 'io.spring.dependency-management' version 'latest.release'
	id 'io.freefair.lombok' version 'latest.release'
	id 'java-library'
	id 'maven-publish'
}

repositories {
	mavenCentral()
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.boot:spring-boot-dependencies:latest.release"
	}
}

dependencies {
	api "org.springframework.boot:spring-boot-autoconfigure"
	api "org.springframework.boot:spring-boot-starter-data-jpa"
	implementation "com.fasterxml.jackson.core:jackson-databind"
	implementation "org.springframework.security:spring-security-core"
	
	testImplementation "org.springframework.boot:spring-boot-starter-test"
	testImplementation "org.springframework.security:spring-security-test"
	testRuntimeOnly "com.h2database:h2"
}

test {
	useJUnitPlatform()
}

java {
	withSourcesJar()
}

publishing {
	publications {
		maven(MavenPublication) {
			from components.java
			versionMapping {
				usage("java-api") {
					fromResolutionOf("runtimeClasspath")
				}
				usage("java-runtime") {
					fromResolutionResult()
				}
			}
		}
	}
	repositories {
		maven {
			if(version.endsWith("-SNAPSHOT")) {
				url "${repoUrlPrefix}snapshots/"
			} else {
				url "${repoUrlPrefix}releases/"
			}
			allowInsecureProtocol true
			credentials {
				username repoUser
				password repoPassword
			}
		}
	}
	
}
